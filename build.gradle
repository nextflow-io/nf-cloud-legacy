/*
 * Copyright 2013-2019, Centre for Genomic Regulation (CRG)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'application'

version = file('VERSION').text.trim()
group = 'io.nextflow'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    // this is required due to this IDEA bug
    // https://youtrack.jetbrains.com/issue/IDEA-129282
    main.output.resourcesDir = 'build/classes/main'
}

idea {
    module.inheritOutputDirs = true
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://repo.eclipse.org/content/groups/releases' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

configurations {
    // see https://docs.gradle.org/4.1/userguide/dependency_management.html#sub:exclude_transitive_dependencies
    all*.exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    all*.exclude group: 'org.codehaus.groovy', module: 'groovy-cli-picocli'
}

dependencies {
    compile 'io.nextflow:nextflow:20.05.7-SNAPSHOT'
    compile 'io.nextflow:nf-amazon:20.05.7-SNAPSHOT'
    compile 'io.nextflow:nf-google:20.05.7-SNAPSHOT'

    compile ('org.apache.ignite:ignite-core:2.4.0')
    compile ('org.apache.ignite:ignite-slf4j:2.4.0')
    compile ('org.apache.ignite:ignite-aws:2.4.0') {
        exclude group: 'com.amazonaws', module: 'aws-java-sdk'
    }
    runtime ('com.amazonaws:aws-java-sdk-s3:1.10.29') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    compile 'com.google.apis:google-api-services-compute:v1-rev20190905-1.30.3'
    compile 'com.google.apis:google-api-services-genomics:v2alpha1-rev20191112-1.30.3'
    compile 'com.google.apis:google-api-services-lifesciences:v2beta-rev20191030-1.30.3'
    compile 'com.google.cloud:google-cloud-nio:0.120.0-alpha'
    compile 'com.google.auth:google-auth-library-oauth2-http:0.18.0'

    /* testCompile inherited from top gradle build file */
    testCompile ('org.apache.ignite:ignite-spring:2.4.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    // test configuration
    testCompile ("org.codehaus.groovy:groovy-test:2.5.11") { exclude group: 'org.codehaus.groovy' }
    testCompile ("cglib:cglib-nodep:2.2")
    testCompile ("org.objenesis:objenesis:2.1")
    testCompile ("org.spockframework:spock-core:1.3-groovy-2.5") { exclude group: 'org.codehaus.groovy' }
    testCompile 'com.google.jimfs:jimfs:1.1'
    // see https://docs.gradle.org/4.1/userguide/dependency_management.html#sec:module_replacement
    modules {
        module("commons-logging:commons-logging") { replacedBy("org.slf4j:jcl-over-slf4j") }
    }

}
